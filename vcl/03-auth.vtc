# NB: Requires Varnishtest 3.0 (see rhargreaves/varnishtest-3.0 docker image)

varnishtest "Auth request and follow redirect"

server s_auth {
	rxreq
	expect req.url == "/protected/?key=client_secret_key_1234"
	txresp -status 302 -hdr "Location: http://protected_domain/content/?key=internal_secret_key_9876"
} -start

server s_origin {
	rxreq
	expect req.url == "/content/?key=internal_secret_key_9876"
	txresp -body "top secret foo"
} -start

varnish v1 -vcl+backend {

	sub vcl_recv {
		if(req.restarts == 0) {
			unset req.http.X-Authed;
		}

		if(!req.http.X-Authed) {
			set req.backend = s_auth;
			return (pass);
		} else {
			set req.backend = s_origin;
			return (lookup);
		}
	}

	sub vcl_deliver {
		if(resp.status == 302 && resp.http.Location ~ "protected_domain") {
			set req.http.X-Authed = "1";
			set req.url = regsub(resp.http.Location,"^http://[^/]+(.*)","\1");
			return(restart);
		}
	}

} -start

client c1 {
	txreq -url "/protected/?key=client_secret_key_1234"
	rxresp
	expect resp.body == "top secret foo"
}

client c1 -run
